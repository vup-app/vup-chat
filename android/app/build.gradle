plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

// define properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file("local.properties")
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader("UTF-8") { reader ->
        localProperties.load(reader)
    }
}

// Define keystore for signing
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    // If the file does not exist, use environment variables (probably in a gh runner)
    keystoreProperties['storeFile'] = 'release-key.jks' ?: "<NONE>"
    keystoreProperties['storePassword'] = System.getenv('KEY_PASSWORD') ?: "<NONE>"
    keystoreProperties['keyAlias'] = 'vup-chat' ?: "<NONE>"
    keystoreProperties['keyPassword'] = System.getenv('KEY_PASSWORD') ?: "<NONE>"
}

android {
    namespace = "chat.vup.app"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion
    project.ext.set("appName", "vup-chat")

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "app.vup.vupchat"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = 1
        versionName = "0.5.20"
        setProperty("archivesBaseName", "$appName-$versionName-$versionCode")
        multiDexEnabled true

    }

    signingConfigs {
       release {
           keyAlias keystoreProperties['keyAlias']
           keyPassword keystoreProperties['keyPassword']
           storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
           storePassword keystoreProperties['storePassword']
       }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    dependencies {
        coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.2'
    }
}

flutter {
    source = "../.."
}
